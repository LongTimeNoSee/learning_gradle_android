apply plugin: 'com.android.application'

dependencies {
    compile 'com.android.support:support-v4:21.0.3'
    compile project(':library:ActionBarSherlock')
    compile project(':library:UnifiedPreference')
}

def hasSigningKey = false

if (project.hasProperty('demoKeyAlias') && 
    project.hasProperty('demoStoreFile') &&
    project.hasProperty('demoStorePassword') &&
    project.hasProperty('demoKeyPassword')) {
    hasSigningKey = true
}

android {
    compileSdkVersion 17
    buildToolsVersion "21.1.1"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 17
    }

    signingConfigs {
        release {
            if (hasSigningKey) {
                keyAlias = demoKeyAlias
                storeFile = file(demoStoreFile)
                storePassword = demoStorePassword
                keyPassword = demoKeyPassword
            }
        }
    }

    productFlavors {
        full {
            applicationId "com.corner23.android.demo.full"
        }

        demo {
            applicationId "com.corner23.android.demo.demo"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        release {
            applicationIdSuffix ".release"
            versionNameSuffix = "-release"
            if (hasSigningKey) {
                signingConfig = signingConfigs.release
            }

            minifyEnabled true
            shrinkResources true
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }
}
