apply plugin: 'com.android.application'

dependencies {
    compile 'com.android.support:support-v4:13.0.0'
    if (project.hasProperty("useRepo")) {
        compile 'com.corner23.android.library:ActionBarSherlock:4.2.0'
        compile 'com.corner23.android.library:UnifiedPreference:0.0.3'
    } else {
        compile project(':library:ActionBarSherlock')
        compile project(':library:UnifiedPreference')
    } 
}

// get hash of current commit
new ByteArrayOutputStream().withStream { os -> 
    def result = exec {
        executable = 'git'
        args = ['rev-parse', '--short', 'HEAD']
        standardOutput = os
    }
    
    project.ext.gitHash = os.toString().trim();
}

archivesBaseName = "demo"

android {
    compileSdkVersion 17
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 17
        versionName '1.0.1'
        applicationId = 'com.corner23.android.demo'
    }

    productFlavors {
        rock
        amazonFree {
            applicationId = 'com.corner23.android.amazon.free'
        }
        playFree {
            applicationId = 'com.corner23.android.playstore.free'
        }
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-SNAPSHOT"
        }

        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            shrinkResources true
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }
}

android.applicationVariants.all { variant ->
    // replace output apk name to <product>-<version>-<buildtype>-<githash>.apk
    def versionSuffix = variant.buildType.versionNameSuffix ? variant.buildType.versionNameSuffix : ""
    def versionName = variant.mergedFlavor.versionName + versionSuffix + "-${gitHash}";

    variant.outputs.each { output->
        def apk = output.getOutputFile();
        def newName = apk.name.replace(variant.buildType.name, versionName);

        output.setOutputFile(new File(apk.parentFile, newName));
    }

    // launch after install
    if (variant.install != null) {
        variant.install.doLast {
            def packageSuffix = variant.buildType.applicationIdSuffix ? variant.buildType.applicationIdSuffix : ""
            def packageName = variant.productFlavors.applicationId[0] ? variant.productFlavors.applicationId[0] : android.defaultConfig.applicationId
            def startupClass = packageName + packageSuffix + '/com.corner23.android.demo.MainActivity'
            def result = exec {
                executable = 'adb'
                args = ['shell', 'am', 'start', '-c', 'android.intent.category.LAUNCHER', '-n', startupClass]
            }
        }
    }
}

if (project.hasProperty('keyAlias') && 
    project.hasProperty('storeFile') &&
    project.hasProperty('storePassword') &&
    project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
